CMAKE_MINIMUM_REQUIRED (VERSION 3.18)

PROJECT ( tmx-message C CXX )

SET (CMAKE_EXPORT_PACKAGE_REGISTRY ON)

ADD_LIBRARY (${PROJECT_NAME} INTERFACE)

FOREACH (MOD_DIR "platform" "types" "codec" "j2735-202007" "asntype")
	MESSAGE (STATUS "Adding sub-component ${MOD_DIR}")
	ADD_SUBDIRECTORY (${MOD_DIR})
ENDFOREACH ()

IF (NOT TMX_J2735)
	SET (TMX_J2735 202007)
ENDIF ()

ADD_LIBRARY (libtmx-j2735 ALIAS tmxasn-j2735-${TMX_J2735})
ADD_LIBRARY (lib${PROJECT_NAME} $<TARGET_OBJECTS:tmxmessage-types>
								$<TARGET_OBJECTS:tmxmessage-codec>
								$<TARGET_OBJECTS:tmxmessage-asntype>)
TARGET_INCLUDE_DIRECTORIES(lib${PROJECT_NAME} PUBLIC
		$<TARGET_PROPERTY:libtmx-j2735,INTERFACE_INCLUDE_DIRECTORIES>
		$<TARGET_PROPERTY:tmxmessage-types,INTERFACE_INCLUDE_DIRECTORIES>
		$<TARGET_PROPERTY:tmxmessage-codec,INTERFACE_INCLUDE_DIRECTORIES>
		$<TARGET_PROPERTY:tmxmessage-asntype,INTERFACE_INCLUDE_DIRECTORIES>
)
TARGET_COMPILE_DEFINITIONS(lib${PROJECT_NAME} PUBLIC TMX_J2735=${TMX_J2735})
TARGET_LINK_OPTIONS (lib${PROJECT_NAME} PUBLIC -Wl,--no-as-needed)
TARGET_LINK_LIBRARIES (lib${PROJECT_NAME} PUBLIC libtmx-j2735 dl backtrace)
TARGET_LINK_LIBRARIES (lib${PROJECT_NAME} PRIVATE ${PROJECT_NAME})
TARGET_LINK_LIBRARIES (lib${PROJECT_NAME} INTERFACE tmxmessage-platform)
SET_TARGET_PROPERTIES (lib${PROJECT_NAME} PROPERTIES PREFIX "")

INSTALL (TARGETS lib${PROJECT_NAME}
		DESTINATION lib
		COMPONENT ${PROJECT_NAME})

EXPORT (PACKAGE ${PROJECT_NAME})

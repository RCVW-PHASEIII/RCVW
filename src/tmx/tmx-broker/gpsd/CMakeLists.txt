CMAKE_MINIMUM_REQUIRED (VERSION 3.18)

PROJECT ( broker-gpsd C CXX )

SET (TMXLIB tmx${PROJECT_NAME})
SET (TMXTEST test-${TMXLIB})

# Check the version of the found GPSD is at least to API 9
SET (GPSD_MIN_API_MAJOR_VERSION 9)

FIND_PATH(Boost_ASIO NAMES boost/asio.hpp HINTS ${Boost_INCLUDE_DIRS})
FIND_PATH (GPS_INCLUDE_DIR NAMES gps.h HINTS /usr /usr/local PATH_SUFFIXES include)

FIND_LIBRARY (GPS_LIBRARY gps)

FILE(STRINGS "${GPS_INCLUDE_DIR}/gps.h" GPSD_API_MAJOR_VERSION REGEX GPSD_API_MAJOR_VERSION)
STRING(REGEX REPLACE "^#define GPSD_API_MAJOR_VERSION[ \t][ \t]*([0-9]*).*" "\\1"
       GPSD_API_MAJOR_VERSION "${GPSD_API_MAJOR_VERSION}")

IF (Boost_ASIO AND ${GPSD_API_MAJOR_VERSION} GREATER_EQUAL ${GPSD_MIN_API_MAJOR_VERSION})
    MESSAGE (STATUS "Including support for GPSD broker")

    FILE (GLOB_RECURSE SOURCES "src/*.c*")

    ADD_LIBRARY (${TMXLIB} OBJECT ${SOURCES})
    TARGET_INCLUDE_DIRECTORIES (${TMXLIB} PUBLIC $<BUILD_INTERFACE:${CMAKE_CURRENT_SOURCE_DIR}/include>
                                                 ${GPS_INCLUDE_DIR})
    TARGET_LINK_LIBRARIES (${TMXLIB} PUBLIC ${GPS_LIBRARY} tmxbroker-api tmxbroker-async)
    TARGET_LINK_LIBRARIES (tmx-broker INTERFACE ${TMXLIB})
ENDIF ()

CMAKE_MINIMUM_REQUIRED (VERSION 3.18)

PROJECT ( tmx-broker C CXX )

SET (CMAKE_EXPORT_PACKAGE_REGISTRY ON)

ADD_LIBRARY (${PROJECT_NAME} INTERFACE)

IF (NOT DEFINED TMX_BROKERS)
    SET (TMX_BROKERS apache async gpsd snmp qpidproton)
ENDIF()

FOREACH (MOD_DIR api ${TMX_BROKERS})
    MESSAGE (STATUS "Adding sub-component ${MOD_DIR}")
    ADD_SUBDIRECTORY (${MOD_DIR})
ENDFOREACH ()

ADD_LIBRARY (lib${PROJECT_NAME} $<TARGET_OBJECTS:tmxbroker-api>)
TARGET_INCLUDE_DIRECTORIES (lib${PROJECT_NAME} PUBLIC $<TARGET_PROPERTY:tmxbroker-api,INTERFACE_INCLUDE_DIRECTORIES>)

FOREACH (BROKER ${TMX_BROKERS})
    IF (TARGET tmxbroker-${BROKER})
        TARGET_SOURCES (lib${PROJECT_NAME} PUBLIC $<TARGET_OBJECTS:tmxbroker-${BROKER}>)
        TARGET_INCLUDE_DIRECTORIES(lib${PROJECT_NAME} PUBLIC
                $<TARGET_PROPERTY:tmxbroker-${BROKER},INTERFACE_INCLUDE_DIRECTORIES>)
    ENDIF()
ENDFOREACH ()

TARGET_LINK_OPTIONS (lib${PROJECT_NAME} PUBLIC -Wl,--no-as-needed)
TARGET_LINK_LIBRARIES (lib${PROJECT_NAME} PUBLIC libtmx-message ${PROJECT_NAME})
SET_TARGET_PROPERTIES (lib${PROJECT_NAME} PROPERTIES PREFIX "")

INSTALL (TARGETS lib${PROJECT_NAME}
        RUNTIME_DEPENDENCIES LIBRARY
        DESTINATION lib
        COMPONENT ${PROJECT_NAME})

EXPORT (PACKAGE ${PROJECT_NAME})

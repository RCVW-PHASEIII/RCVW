#
# V2X Hub Plugins
# Makefile configuration script
#
# Author:
#    Gregory M. Baumgardner
# Version:
#    1.0 : Initial version
#    2.0 : Release 4.0.0 updates
#

CMAKE_MINIMUM_REQUIRED (VERSION 3.18)

PROJECT (rcvw LANGUAGES NONE VERSION 4.0.0 DESCRIPTION "Rail Crossing Violation Warning")

IF (COMMAND SETUP_TMX_PROJECT)
    SETUP_TMX_PROJECT ()
ENDIF ()

ENABLE_LANGUAGE ( C )
ENABLE_LANGUAGE ( CXX )

INCLUDE (CheckCCompilerFlag)
CHECK_C_COMPILER_FLAG ("-fPIC" CMAKE_C_FLAG_FPIC_SUPPORTED)

IF (NOT TMX_BIN_DIR)
    SET (TMX_BIN_DIR "${CMAKE_BINARY_DIR}/bin")
ENDIF (NOT TMX_BIN_DIR)

IF (NOT TMX_LIB_DIR)
    SET (TMX_LIB_DIR "${CMAKE_BINARY_DIR}/lib")
ENDIF (NOT TMX_LIB_DIR)

IF (TMX_CROSSCOMPILING)
    MESSAGE (STATUS "Cross-compiling for ${CMAKE_SYSTEM} on ${CMAKE_SYSTEM_PROCESSOR}")
ENDIF (TMX_CROSSCOMPILING)

GET_FILENAME_COMPONENT (TMX_SOURCE_DIR ${CMAKE_CURRENT_SOURCE_DIR}/../tmx ABSOLUTE)
GET_FILENAME_COMPONENT (TMX_BINARY_DIR ${CMAKE_CURRENT_BINARY_DIR}/../tmx ABSOLUTE)

ADD_SUBDIRECTORY(${TMX_SOURCE_DIR} ${TMX_BINARY_DIR})

INCLUDE_DIRECTORIES(${CMAKE_CURRENT_SOURCE_DIR}/../v2x-hub/ICD/include)

ADD_SUBDIRECTORY(../v2x-hub ${CMAKE_BINARY_DIR}/v2x-hub)

UNSET (BUILD_SAMPLES CACHE)
SET (BUILD_SAMPLES OFF CACHE BOOL "")
ADD_SUBDIRECTORY (AIOUSB/AIOUSB)

FOREACH (Plugin "HRIStatusPlugin" "RCVWPlugin" "DifferentialGPSPlugin" "CVInspectorPlugin")
    IF (NOT SKIP_${Plugin})
        ADD_SUBDIRECTORY (${Plugin})
    ENDIF ()
ENDFOREACH ()

INSTALL (DIRECTORY ${CMAKE_CURRENT_SOURCE_DIR}/../tmx-invehicleportal/${PROJECT_NAME}/
         DESTINATION /var/www/html/${PROJECT_NAME}
         COMPONENT ${PROJECT_NAME}hmi)
INSTALL (FILES ${CMAKE_CURRENT_SOURCE_DIR}/../tmx-invehicleportal/index.html
         DESTINATION /var/www/html
         COMPONENT ${PROJECT_NAME}hmi)
